<!DOCTYPE html>
<html lang="en-us">
<head>
  <title>So you want to write hot code reload, eh?</title>
  <link rel="icon" href="data:image/svg+xml,<svg xmlns=%22http://www.w3.org/2000/svg%22 viewBox=%220 0 100 100%22><text y=%22.9em%22 font-size=%2280%22>üê≥</text></svg>">
  <meta content="text/html; charset=utf-8" http-equiv="content-type">
  <meta content="width=device-width, initial-scale=1" name="viewport">
  <meta content="nimib 0.3.12" name="generator">
  <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/water.css@2/out/dark.min.css">
  <link rel='stylesheet' href='https://cdn.jsdelivr.net/gh/pietroppeter/nimib/assets/androidstudio.css'>
    <script src="https://cdn.jsdelivr.net/gh/pietroppeter/nimib@main/assets/highlight.min.js"></script>
<script>hljs.highlightAll();</script>

  <style>
.nb-box {
  display: flex;
  align-items: center;
  justify-content: space-between;
}
.nb-small {
  font-size: 0.8rem;
}
button.nb-small {
  float: right;
  padding: 2px;
  padding-right: 5px;
  padding-left: 5px;
}
section#source {
  display:none
}
pre > code {
  font-size: 1.2em;
}
.nb-output {
  line-height: 1.15;
}
figure {
  margin: 2rem 0;
}
figcaption {
  text-align: center;
}
  
</style>
  
</head>
<body>
<header>
<div class="nb-box">
  <span><a href="../writeups.html">üè°</a></span>
  <span><code>hcr.nim</code></span>
  <span></span>
</div>
<hr>
</header><main>
<h1>It is not hot it is atleast lukewarm</h1>
<p>One of the most desirable features of a game development environment is the ability to change code and see the changes live.
Vexingly many game developers use a statically typed compiled language making hot code reload more difficult.
As someone that went though the work this documents the journey across the <a href="https://www.github.com/beef331/potato">potato</a> fields.</p>
<h2>Dynamic libraries - I swear they just keep moving on me.</h2>
<p>For those not in the know a dynamic library is a compiled blob of code that you can load at runtime.
Enabling both the ability to add new code and to replace implementations.
They have a symbol table which lets users search for and access procedures and variables once loaded.
It is this which will allow us to write hot code reload.
The game code can be compiled into a dynamic library and be loaded by a host program.
Allowing the programmer to change code then recompile and continue where left off.</p>
<pre>hcr1host.nim</pre>
<pre><code class="nim hljs">import std/dynlib

type Entry = proc() {.cdecl.}

var lib = loadLib(&quot;./libhcr.so&quot;)
let entry = cast[Entry](lib.symAddr(&quot;entry&quot;))
entry()
</code></pre>

<pre>hcr1lib.nim</pre>
<pre><code class="nim hljs">proc entry() {.exportc, dynlib.} = echo &quot;Hello&quot;
</code></pre>

<pre>hcr1lib.nims</pre>
<pre><code class="nims hljs">--app:lib

when defined(linux):
  --o:&quot;libhcr.so&quot;
elif defined(windows):
  --o:&quot;libhcr.dll&quot;
else:
  --o:&quot;libhcr.dylib&quot;
</code></pre>

<p>This is a basic program with a pluggable function.
When <code>hcr1host</code> runs it loads the library <code>./libhcr.so</code> then it calls the <code>entry</code> procedure declared there.
Those smart enough to always plug in a USB type-A first try can see where this takes us.
The first step to that quest is to have a loop that watches the file.
Here cause I prefer brevity will resort to a simple watcher(A cross platform solution that will make anyone knowledgable cry and even whimper.).</p>
<pre>hcr2host.nim</pre>
<pre><code class="nim hljs">import std/[dynlib, times, os]

type Entry = proc() {.cdecl.}

const libPath = 
  when defined(linux):
    &quot;./libhcr.so&quot;
  elif defined(windows):
    &quot;./libhcr.dll&quot;
  else:
    &quot;./libhcr.dylib&quot;



var lastLoad = default Time

while true:
  let thisLoad = 
    try:
      getLastModificationTime(libPath)
    except CatchableError:
      continue


  if lastLoad &lt; thisLoad:
    lastLoad = thisLoad
    var lib =
      try:
        loadLib(libPath)
      except CatchableError as e:
        echo &quot;Failed to load dynamic library: &quot;, e.msg
        continue

    if lib == nil:
      echo &quot;Failed to load lib: &quot;, libPath
    else:
      let entry = cast[Entry](lib.symAddr(&quot;entry&quot;))
      if entry == nil:
        echo &quot;No function named 'entry'&quot;
        if lib != nil:
          lib.unloadLib()
        continue
      entry()
      lib.unloadLib()

</code></pre>

<p>With this one can modify the <code>hcr1lib.nim</code> and recompile it and without closing <code>hcr2host</code> will see a live change.
Tinkering with it you may notice a problem that sticks out like a toe in a ripped sock.
Nothing persists on reload.</p>
<pre>hcr2lib.nim</pre>
<pre><code class="nim hljs">var i = 0
proc entry() {.exportc, dynlib.} =
  inc i
  echo i 
</code></pre>

<p>Using this as the library every reload prints <code>1</code> and it does not persist.
This means one needs to store global state somehow... the best way of doing that is you guessed it dynamic symbols!
Not only does a dynamic library add to a symbol table the host program also does.
To do this one needs to invent a <code>saveInt</code> procedure which lets the library save and reload state.
Though a cookie jar is only good for storage cause you can get cookies out which means it also needs a <code>getInt</code> procedure to fetch the value stored.</p>
<pre>hcr3host.nim</pre>
<pre><code class="nim hljs">import std/[dynlib, times, os, tables]

{.passc: &quot;-rdynamic&quot;, passL: &quot;-rdynamic&quot;.} ## Needed so we can access symbols from children

type Entry = proc() {.cdecl.}

const libPath = 
  when defined(linux):
    &quot;./libhcr.so&quot;
  elif defined(windows):
    &quot;./libhcr.dll&quot;
  else:
    &quot;./libhcr.dylib&quot;

var ints: Table[string, int]

proc saveInt(name: string, i: int) {.exportc, dynlib, raises: [].} =
  ints[name] = i

proc getInt(name: string, i: var int) : bool {.exportc, dynlib, raises: [].} =
  if name in ints:
    try:
      i = ints[name]
      true
    except CatchableError:
      false
  else:
    false


var lastLoad = default Time

while true:
  let thisLoad = 
    try:
      getLastModificationTime(libPath)
    except CatchableError:
      continue


  if lastLoad &lt; thisLoad:
    lastLoad = thisLoad
    var lib =
      try:
        loadLib(libPath)
      except CatchableError as e:
        echo &quot;Failed to load dynamic library: &quot;, e.msg
        continue

    if lib == nil:
      echo &quot;Failed to load lib: &quot;, libPath
    else:
      let entry = cast[Entry](lib.symAddr(&quot;entry&quot;))
      if entry == nil:
        echo &quot;No function named 'entry'&quot;
        if lib != nil:
          lib.unloadLib()
        continue
      entry()
      lib.unloadLib()

</code></pre>

<p>Nim specific but exceptions do not raise across dynamic library barriers so it is best to return a bool as we do here.
This allows the reloading code to set a default value if we do not load.
With C <code>-rdynamic</code> is required to be able to access the host procedures from the dynamic library</p>
<pre>hcr3lib.nim</pre>
<pre><code class="nim hljs">proc saveInt(name: string, val: int) {.importc, dynlib&quot;&quot;.}
proc getInt(name: string, val: var int): bool {.importc, dynlib&quot;&quot;.}

var i = 0
if not getInt(&quot;i&quot;, i):
  i = 0 # Redundant but let's stay classy 

proc entry() {.exportc, dynlib.} =
  inc i
  echo i
  saveInt(&quot;i&quot;, i)
</code></pre>

<p>More Nim specific details <code>dynlib</code> loads using <code>dlopen</code> which means we can supply <code>&quot;&quot;</code> and it will load from this program's symbol table.
In less nerdy POSIX talk it means it will load <code>getInt</code> and <code>setInt</code> from the host program.</p>
<p>With all this now one can easily replace their dynamic library and now they can reload integers!
This can be expanded to use tagged unions instead to enable storing more complex state(infact in Potato deeply nested structures save just fine!)</p>
<h2>Serialization</h2>
<p>The heart of storing state across reloads is ensuring the old data can be migrated to the most recent binary with possible new fields added.
In this case it means one will use a tagged union across primitive types.
Meaning we need a single data type that can hold <code>int</code>, <code>float</code>, <code>string</code>, a list, and a structure.</p>
<pre><code class="language-nim">import std/tables

type
  HcrKind = enum
    Int
    Float
    String
    Array
    Struct

  HcrObj = object
    case kind: HcrKind
    of Int:
      i: int
    of Float:
      f: float
    of String:
      s: string
    of Array:
      arr: seq[HcrObj]
    of Struct:
      fields: Table[string, HcrObj]
</code></pre>
<p>This data type is sufficient to store every type under the sun (though in the case of Potato Nim's <code>std/json.JsonNode</code> is just used).
To enable support of reference types all data of a structure type should be stored to a root level <code>HcrObj</code> where the entry object is at a field named <code>data</code>.
This allows storing references in the top level using their old pointer value as a field name.</p>
<p>With that the following is how mapping between types works:</p>
<ul>
<li>Any integer, enum, char, or bool maps to <code>Int</code></li>
<li>32bit and 64bit floats map to <code>Float</code></li>
<li><code>string</code> maps to <code>String</code></li>
<li><code>ptr T</code>, <code>pointer</code>, and <code>proc</code> map to <code>Int</code></li>
<li><code>ref T</code> stores as an <code>Int</code> in place. Though adds to the root object's fields at its old pointer value. On load a table of the old pointer to new must be stored to migrate to new pointers.</li>
<li><code>seq[T]</code> and <code>array[Idx, T]</code> map to <code>Array</code></li>
<li><code>set[T]</code> maps to <code>String</code> just allocate a string which is <code>sizeof(set[T]))</code> and copy the memory over</li>
<li><code>object</code> and <code>tuple</code> map to <code>Struct</code>, iterate the fields and store to <code>fields</code></li>
</ul>
<p>It is also very important to note that since pointers are assumed to be valid after reload the old library must not be unloaded.
This will leak memory but it also ensures the pointers are still pointing to alive data.
Doing this will also force all pointer procedures to be reloaded on program reload as migrating pointer procedures is not a fun problem.
Pointer procedures to named procedures is relative easy to migrate, but any anonymous procedure is mangled in such a way you cannot be certain two procedures with the same name are the same.
For intuitive hot code reload it is best to avoid pointer procedures and use some sort of global memory or vtable instead, that way it can be reinitialised and the procedures will be updated.</p>
<p>To enable saving before reload one should also create a list of serializers to store all global variables to the host program.</p>
<pre><code class="language-nim"># Inside the library
var serializers: seq[proc()]
proc hcrSave() {.exportc, dynlib.} =
  for serializer in serializers:
    serializer()

var someInt = 0
serializers.add proc() =
  saveInt(&quot;someInt&quot;, someInt)
</code></pre>
<p><code>hcrSave</code> can then be called before reload on the host to save the state.
Which means when the next library is loaded it will fetch the memory stored in the host and continue as if nothing happened.</p>
<h2>Signals</h2>
<p>If a loaded program crashes one should not have to relaunch the program.
It likely should continue where it left off rerunning the frame.
To achieve this one can use signal handlers to call a <code>hcrError</code> procedure from the host program.
This <code>hcrError</code> will use <code>siglongjmp</code> to return the program back to before the loop was called and let the program continue again.</p>
<h2>The rest of the Tyto</h2>
<p>Finally with all that work the following is practically what one will see when they build the system following this writeup.
Though of course expansion for serializers using the <code>HcrObj</code> should be done.</p>
<pre>hcr4host.nim</pre>
<pre><code class="nim hljs">import std/[dynlib, times, os, tables, tempfiles]
import system/ansi_c

{.passc: &quot;-rdynamic&quot;, passL: &quot;-rdynamic&quot;.} ## Needed so we can access symbols from children

type Entry = proc() {.cdecl.}

const libPath = 
  when defined(linux):
    &quot;./libhcr.so&quot;
  elif defined(windows):
    &quot;./libhcr.dll&quot;
  else:
    &quot;./libhcr.dylib&quot;

var ints: Table[string, int]

proc saveInt(name: string, i: int) {.exportc, dynlib, raises: [].} =
  ints[name] = i

proc getInt(name: string, i: var int) : bool {.exportc, dynlib, raises: [].} =
  if name in ints:
    try:
      i = ints[name]
      true
    except CatchableError:
      false
  else:
    false

const
  ErrorJump = 1
  QuitJump = 2

type sigjmp_buf {.bycopy, importc: &quot;sigjmp_buf&quot;, header: &quot;&lt;setjmp.h&gt;&quot;.} =  object

proc sigsetjmp(jmpb: C_JmpBuf, savemask: cint): cint {.header: &quot;&lt;setjmp.h&gt;&quot;, importc: &quot;sigsetjmp&quot;.}
proc siglongjmp(jmpb: C_JmpBuf, retVal: cint) {.header: &quot;&lt;setjmp.h&gt;&quot;, importc: &quot;siglongjmp&quot;.}

var jmp: C_JmpBuf

proc hcrError() {.exportc, dynlib.} =
  siglongjmp(jmp, ErrorJump)

proc hcrQuit() {.exportc, dynlib.} =
  siglongjmp(jmp, QuitJump)


var 
  lastLoad = default Time
  crashed = false
  lib: LibHandle
  entry: Entry

while true:
  let thisLoad = 
    try:
      getLastModificationTime(libPath)
    except CatchableError:
      continue


  if lastLoad &lt; thisLoad:
    lastLoad = thisLoad
    if lib != nil:
      cast[proc(){.nimcall.}](lib.symAddr(&quot;hcrSave&quot;))()

    lib =
      try:
        let tempLibPath = genTempPath(&quot;someLib&quot;, &quot;.so&quot;) # Move it so we can reload it Operating Systems can be funky
        copyFile(libPath, tempLibPath)
        loadLib(tempLibPath, false) # Do load symbols to global table
      except CatchableError as e:
        echo &quot;Failed to load dynamic library: &quot;, e.msg
        continue

    if lib == nil:
      echo &quot;Failed to load lib: &quot;, libPath
      continue

    entry = cast[Entry](lib.symAddr(&quot;entry&quot;))
    if entry == nil:
      echo &quot;No function named 'entry'&quot;
      if lib != nil:
        lib.unloadLib()
        lib = nil
        continue
    echo &quot;Loaded new lib&quot;

  if lib != nil and entry != nil:
    case sigsetjmp(jmp, int32.high.cint)
    of 0:
      entry()
    of ErrorJump:
      crashed = true
      echo &quot;Crashed&quot;
    of QuitJump:
      echo &quot;Quit&quot;
      break
    else:
      echo &quot;Incorrect jump&quot;

  sleep(16) # Pretend we're doing work like a game
</code></pre>

<pre>hcr4lib.nim</pre>
<pre><code class="nim hljs">import system/ansi_c
proc hcrError() {.importc, dynlib&quot;&quot;.}
proc hcrQuit() {.importc, dynlib&quot;&quot;.}

var serializers: seq[proc()]
proc hcrSave() {.exportc, dynlib.} =
  for serializer in serializers:
    serializer()


unhandledExceptionHook = proc(e: ref Exception) {.nimcall, gcsafe, raises: [], tags: [].}=
  try:
    {.cast(tags: []).}:
      for i, x in e.getStackTraceEntries:
        stdout.write x.fileName, &quot;(&quot;, x.line, &quot;) &quot;, x.procName
        stdout.write &quot;\n&quot;
      stdout.write&quot;Error: &quot;
      stdout.writeLine e.msg
      stdout.flushFile()
      hcrError()
  except:
    discard


{.push stackTrace:off.}
proc signalHandler(sign: cint) {.noconv.} =
  if sign == SIGINT:
    hcrQuit()
    hcrError()
  elif sign == SIGSEGV:
    writeStackTrace()
    echo &quot;SIGSEGV: Illegal storage access. (Attempt to read from nil?)&quot;
    hcrError()
  elif sign == SIGABRT:
    writeStackTrace()
    echo &quot;SIGABRT: Abnormal termination.&quot;
    hcrError()
  elif sign == SIGFPE:
    writeStackTrace()
    echo &quot;SIGFPE: Arithmetic error.&quot;
    hcrError()
  elif sign == SIGILL:
    writeStackTrace()
    echo &quot;SIGILL: Illegal operation.&quot;
    hcrError()
  elif (when declared(SIGBUS): sign == SIGBUS else: false):
    echo &quot;SIGBUS: Illegal storage access. (Attempt to read from nil?)&quot;
    hcrError()
{.pop.}

#c_signal(SIGINT, signalHandler)
c_signal(SIGSEGV, signalHandler)
c_signal(SIGABRT, signalHandler)
c_signal(SIGFPE, signalHandler)
c_signal(SIGILL, signalHandler)
when declared(SIGBUS):
  c_signal(SIGBUS, signalHandler)



proc saveInt(name: string, val: int) {.importc, dynlib&quot;&quot;.}
proc getInt(name: string, val: var int): bool {.importc, dynlib&quot;&quot;.}

var i = 0
if not getInt(&quot;i&quot;, i):
  i = 0 # Redundant but let's stay classy 

serializers.add proc() = saveInt(&quot;i&quot;, i)


proc entry() {.exportc, dynlib.} =
  inc i
  echo i
  if i &gt;= 10000:
    hcrQuit() # Leave this place
</code></pre>

<pre>hcr4lib.nims</pre>
<pre><code class="nims hljs">--app:lib
--nimMainPrefix:&quot;hcr&quot;
when defined(linux):
  --o:&quot;libhcr.so&quot;
elif defined(windows):
  --o:&quot;libhcr.dll&quot;
else:
  --o:&quot;libhcr.dylib&quot;
</code></pre>

<h2>Closing</h2>
<p>Thanks for reading.
More information can be found by reading the source code of <a href="https://github.com/beef331/potato">Potato</a>.
Including things not touched on here like how to use a single module for both the host and library.</p>
</main>
<footer>
<div class="nb-box">
  <span><span class="nb-small">made with <a href="https://pietroppeter.github.io/nimib/">nimib üê≥</a></span></span>
  <span></span>
  <span><button class="nb-small" id="show" onclick="toggleSourceDisplay()">Show Source</button></span>
</div>
</footer>
<section id="source">
<pre><code class="nohighlight nim hljs"><span class="hljs-keyword">import</span> nimib
nbInit()
nb.context[<span class="hljs-string">&quot;path_to_root&quot;</span>] = <span class="hljs-string">&quot;../writeups.html&quot;</span>
nb.partials[<span class="hljs-string">&quot;header_right&quot;</span>] = <span class="hljs-string">&quot;&quot;</span>
nb.context[<span class="hljs-string">&quot;github_remote_url&quot;</span>] = <span class="hljs-string">&quot;&quot;</span>
nb.context[<span class="hljs-string">&quot;github_logo&quot;</span>] = <span class="hljs-string">&quot;&quot;</span>


nb.darkMode()
nb.title = <span class="hljs-string">&quot;So you want to write hot code reload, eh?&quot;</span>
nbText:<span class="hljs-string">&quot;&quot;&quot;
# It is not hot it is atleast lukewarm

One of the most desirable features of a game development environment is the ability to change code and see the changes live.
Vexingly many game developers use a statically typed compiled language making hot code reload more difficult.
As someone that went though the work this documents the journey across the [potato](https://www.github.com/beef331/potato) fields.

## Dynamic libraries - I swear they just keep moving on me.

For those not in the know a dynamic library is a compiled blob of code that you can load at runtime.
Enabling both the ability to add new code and to replace implementations.
They have a symbol table which lets users search for and access procedures and variables once loaded.
It is this which will allow us to write hot code reload.
The game code can be compiled into a dynamic library and be loaded by a host program.
Allowing the programmer to change code then recompile and continue where left off.
&quot;&quot;&quot;</span>

<span class="hljs-keyword">proc</span> nbFile(name: <span class="hljs-built_in">string</span>) =
  nbFile(name, readFile(name))


nbFile(<span class="hljs-string">&quot;hcr1host.nim&quot;</span>)
nbFile(<span class="hljs-string">&quot;hcr1lib.nim&quot;</span>)
nbFile(<span class="hljs-string">&quot;hcr1lib.nims&quot;</span>)

nbText:<span class="hljs-string">&quot;&quot;&quot;
This is a basic program with a pluggable function.
When `hcr1host` runs it loads the library `./libhcr.so` then it calls the `entry` procedure declared there.
Those smart enough to always plug in a USB type-A first try can see where this takes us.
The first step to that quest is to have a loop that watches the file.
Here cause I prefer brevity will resort to a simple watcher(A cross platform solution that will make anyone knowledgable cry and even whimper.).
&quot;&quot;&quot;</span>

nbFile(<span class="hljs-string">&quot;hcr2host.nim&quot;</span>)

nbText:<span class="hljs-string">&quot;&quot;&quot;
With this one can modify the `hcr1lib.nim` and recompile it and without closing `hcr2host` will see a live change.
Tinkering with it you may notice a problem that sticks out like a toe in a ripped sock.
Nothing persists on reload.
&quot;&quot;&quot;</span>

nbFile(<span class="hljs-string">&quot;hcr2lib.nim&quot;</span>)

nbText:<span class="hljs-string">&quot;&quot;&quot;
Using this as the library every reload prints `1` and it does not persist.
This means one needs to store global state somehow... the best way of doing that is you guessed it dynamic symbols!
Not only does a dynamic library add to a symbol table the host program also does.
To do this one needs to invent a `saveInt` procedure which lets the library save and reload state.
Though a cookie jar is only good for storage cause you can get cookies out which means it also needs a `getInt` procedure to fetch the value stored.
&quot;&quot;&quot;</span>

nbFile(<span class="hljs-string">&quot;hcr3host.nim&quot;</span>)

nbText:<span class="hljs-string">&quot;&quot;&quot;
Nim specific but exceptions do not raise across dynamic library barriers so it is best to return a bool as we do here.
This allows the reloading code to set a default value if we do not load.
With C `-rdynamic` is required to be able to access the host procedures from the dynamic library
&quot;&quot;&quot;</span>

nbFile(<span class="hljs-string">&quot;hcr3lib.nim&quot;</span>)

nbText:<span class="hljs-string">&quot;&quot;&quot;
More Nim specific details `dynlib` loads using `dlopen` which means we can supply `&quot;&quot;` and it will load from this program's symbol table.
In less nerdy POSIX talk it means it will load `getInt` and `setInt` from the host program.

With all this now one can easily replace their dynamic library and now they can reload integers!
This can be expanded to use tagged unions instead to enable storing more complex state(infact in Potato deeply nested structures save just fine!)

## Serialization

The heart of storing state across reloads is ensuring the old data can be migrated to the most recent binary with possible new fields added.
In this case it means one will use a tagged union across primitive types.
Meaning we need a single data type that can hold `int`, `float`, `string`, a list, and a structure.

```nim
import std/tables

type
  HcrKind = enum
    Int
    Float
    String
    Array
    Struct

  HcrObj = object
    case kind: HcrKind
    of Int:
      i: int
    of Float:
      f: float
    of String:
      s: string
    of Array:
      arr: seq[HcrObj]
    of Struct:
      fields: Table[string, HcrObj]
```  


This data type is sufficient to store every type under the sun (though in the case of Potato Nim's `std/json.JsonNode` is just used).
To enable support of reference types all data of a structure type should be stored to a root level `HcrObj` where the entry object is at a field named `data`.
This allows storing references in the top level using their old pointer value as a field name.


With that the following is how mapping between types works:

- Any integer, enum, char, or bool maps to `Int`
- 32bit and 64bit floats map to `Float`
- `string` maps to `String`
- `ptr T`, `pointer`, and `proc` map to `Int`
- `ref T` stores as an `Int` in place. Though adds to the root object's fields at its old pointer value. On load a table of the old pointer to new must be stored to migrate to new pointers.
- `seq[T]` and `array[Idx, T]` map to `Array`
- `set[T]` maps to `String` just allocate a string which is `sizeof(set[T]))` and copy the memory over
- `object` and `tuple` map to `Struct`, iterate the fields and store to `fields`

It is also very important to note that since pointers are assumed to be valid after reload the old library must not be unloaded.
This will leak memory but it also ensures the pointers are still pointing to alive data.
Doing this will also force all pointer procedures to be reloaded on program reload as migrating pointer procedures is not a fun problem.
Pointer procedures to named procedures is relative easy to migrate, but any anonymous procedure is mangled in such a way you cannot be certain two procedures with the same name are the same.
For intuitive hot code reload it is best to avoid pointer procedures and use some sort of global memory or vtable instead, that way it can be reinitialised and the procedures will be updated.

To enable saving before reload one should also create a list of serializers to store all global variables to the host program.

```nim
# Inside the library
var serializers: seq[proc()]
proc hcrSave() {.exportc, dynlib.} =
  for serializer in serializers:
    serializer()

var someInt = 0
serializers.add proc() =
  saveInt(&quot;someInt&quot;, someInt)
```
`hcrSave` can then be called before reload on the host to save the state.
Which means when the next library is loaded it will fetch the memory stored in the host and continue as if nothing happened.

## Signals

If a loaded program crashes one should not have to relaunch the program.
It likely should continue where it left off rerunning the frame.
To achieve this one can use signal handlers to call a `hcrError` procedure from the host program.
This `hcrError` will use `siglongjmp` to return the program back to before the loop was called and let the program continue again.

## The rest of the Tyto

Finally with all that work the following is practically what one will see when they build the system following this writeup.
Though of course expansion for serializers using the `HcrObj` should be done.
&quot;&quot;&quot;</span>




nbFile(<span class="hljs-string">&quot;hcr4host.nim&quot;</span>)
nbFile(<span class="hljs-string">&quot;hcr4lib.nim&quot;</span>)
nbFile(<span class="hljs-string">&quot;hcr4lib.nims&quot;</span>)

<span class="hljs-string">nbText&quot;&quot;&quot;
## Closing
Thanks for reading.
More information can be found by reading the source code of [Potato](https://github.com/beef331/potato).
Including things not touched on here like how to use a single module for both the host and library.
&quot;&quot;&quot;</span>

nbSave()
</code></pre>
</section><script>
function toggleSourceDisplay() {
  var btn = document.getElementById("show")
  var source = document.getElementById("source");
  if (btn.innerHTML=="Show Source") {
    btn.innerHTML = "Hide Source";
    source.style.display = "block";
  } else {
    btn.innerHTML = "Show Source";
    source.style.display = "none";
  }
}
</script></body>
</html>